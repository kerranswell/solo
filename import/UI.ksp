import "import/UI_Hits.ksp"

macro UI_on_init

    family UI
        declare read ui_switch softMute
        declare ui_switch rh_Muted
        declare ui_switch lh_Vibrato
        declare ui_switch altStroke
        declare ui_switch letRing
        declare ui_switch pickVelocityControl
        declare ui_button debugClear
        declare ui_knob attackSoftness (0, 100, 1)
        // declare ui_knob vibratoSpeed (-50, 50, 1)
        declare read ui_knob vibratoSpeed (0, 100, 1)
        declare ui_knob vibratoDepth (0, 100, 1)
        declare ui_label label (2,6)
        declare ui_label label2 (2,6)
        declare ui_knob pos(0,Const.fretCount-3, 1)

        declare ui_button fretboard
        declare &_Hit hits[MAX_SIMULTANEOUS_NOTES]
    end family

	define FRETBOARD_X := 10
	define FRETBOARD_Y := 170

	UI.fretboard -> value := 1
	UI.fretboard -> text := ""
	mUI_size(UI.fretboard, 600, 71)
	set_control_par_str(get_ui_id(UI.fretboard), $CONTROL_PAR_PICTURE, "fretboard")
	mUI_move(UI.fretboard, FRETBOARD_X, FRETBOARD_Y)

	UI_Hits_on_init()
   
    set_text(UI.label,"") 
    set_text(UI.label2,"") 

    UI.softMute := 0
    set_text(UI.softMute, "Soft P.M.")

    UI.rh_Muted := playModeGlobal.pm.rh_Muted
    set_text(UI.rh_Muted, "P.M.")

    UI.lh_Vibrato := playModeGlobal.pm.lh_Vibrato
    set_text(UI.lh_Vibrato, "Vibrato")

    UI.altStroke := playModeGlobal.altStroke
    set_text(UI.altStroke, "Alt Stroke")

    UI.letRing := playModeGlobal.pm.letRing
    set_text(UI.letRing, "Let Ring")

    UI.pickVelocityControl := 1
    if (playModeGlobal.altStroke = 1)
        UI.pickVelocityControl := 0
    end if
    set_text(UI.pickVelocityControl, "Velocity Control")

    UI.attackSoftness := 0
    set_text(UI.attackSoftness, "Att. Softness")
    
    UI.vibratoSpeed := VIBRATO_SPEED_DEFAULT
    set_text(UI.vibratoSpeed, "Vib. Speed")
    set_knob_defval(UI.vibratoSpeed, VIBRATO_SPEED_DEFAULT)

    UI.vibratoDepth := 0
    set_text(UI.vibratoDepth, "Vib. Depth")
    set_knob_defval(UI.vibratoDepth, 0)

    set_text(UI.debugClear, "Debug Clear")
    UI.debugClear := 0

    set_text(UI.pos, "Pos")
    UI.pos := playModeGlobal.pos
    set_knob_defval(UI.pos, UI.pos)	
end macro

macro mUI_hide(#o#)
	set_control_par(#o#, $CONTROL_PAR_HIDE, $HIDE_WHOLE_CONTROL) 	
end macro
macro mUI_show(#o#)
	set_control_par(#o#, $CONTROL_PAR_HIDE, $HIDE_PART_NOTHING)
end macro

macro mUI_move(#o#, x, y)
	#o# -> pos_x := x
	#o# -> pos_y := y
end macro

macro mUI_size(#o#, w, h)
	#o# -> width := w
	#o# -> height := h
end macro

function UI_playNote(i)
	UI_Hit_activateNew(i)
end function

function UI_stopNote(i)
	declare j
	j := UI_Hit_getIndexByMaster(i)
	if (j >= 0)
		UI_Hit_reset(j)
	end if
end function