on ui_control (UI.rh_Muted)
    extControl(ExtEventType.rh_Muted, UI.rh_Muted)
end on

on ui_control (UI.lh_Vibrato)
    extControl(ExtEventType.lh_Vibrato, UI.lh_Vibrato)
end on

on ui_control (UI.altStroke)
    extControl(ExtEventType.altStroke, UI.altStroke)
end on

on ui_control (UI.letRing)
    extControl(ExtEventType.letRing, UI.letRing)
end on


on ui_control (UI.vibratoSpeed)
    extControl(ExtEventType.vibratoSpeed, UI.vibratoSpeed)
end on

on ui_control (UI.vibratoDepth)
    extControl(ExtEventType.vibratoDepth, UI.vibratoDepth)
end on

on ui_control (UI.debugClear)
    debug_clear()
    debug2_clear()

    UI.debugClear := 0
end on

macro macroTriggerPTCallbacks(#n#)
    on ui_control (UI.triggerPT#n#)
        extControl(ExtEventType.triggerPT, #n#+1)

        UI.triggerPT#n# := 0
    end on
end macro

iterate_macro(macroTriggerPTCallbacks) := 0 to 6

function extControl(etype, value)

    declare ev
    declare i

    select etype

        case ExtEventType.letRing
            playModeGlobal.letRing := value

        case ExtEventType.rh_Muted
            playModeGlobal.rh_Muted := value

        case ExtEventType.altStroke
            playModeGlobal.altStroke := value

        case ExtEventType.lh_Vibrato
            playModeGlobal.lh_Vibrato := value

        case ExtEventType.vibratoSpeed
            playModeGlobal.vibratoSpeed := value

        case ExtEventType.vibratoDepth
            if (playModeGlobal.vibratoDepth = 0 and value > 0)
                // starting vibrato cycle
                for i := 0 to Globals.maxVoices-1
                    if (Playing[i].sys # -1 and Playing[i].ghost = 0)
                        Playing_startVibratoCycle(i)
                    end if
                end for
            else if (playModeGlobal.vibratoDepth > 0 and value = 0)
                // ending vibrato cycle
                for i := 0 to Globals.maxVoices-1
                    if (Playing[i].sys # -1 and Playing[i].ghost = 0 and Playing[i].timeOp.vibrato # -1)
                        Playing[i].timeOp.vibrato := -1
                        change_tune(Playing[i].sys, 0, 0)
                    end if
                end for                
            end if

            playModeGlobal.vibratoDepth := value

        case ExtEventType.triggerPT
            declare string
            string := value
            for i := 0 to Globals.maxVoices-1
                if (Playing[i].sys # -1 and Playing[i].ghost = 0)

                    // starting Time operation
                    Playing_startPitchTableCycle(i, string)
                end if
            end for



{        case ExtEventType.vibratoSpeed

            declare group_index

            for i := 1 to Globals.takeCounts[ArtsCodes.v1]
                group_index := find_group("v1_t" & i)
                ev := Math.epSpeed(Math.V2E, (value + 100) * 10)
                set_engine_par($ENGINE_PAR_SPEED, ev, group_index, -1, -1)
            end for
}
    end select

    uiFeedback(etype, value)
end function

function uiFeedback(etype, value)
    if (etype = ExtEventType.letRing and UI.letRing # value)
        UI.letRing := value
    else if (etype = ExtEventType.rh_Muted and UI.rh_Muted # value)
        UI.rh_Muted := value
    else if (etype = ExtEventType.altStroke and UI.altStroke # value)
        UI.altStroke := value
    else if (etype = ExtEventType.lh_Vibrato and UI.lh_Vibrato # value)
        UI.lh_Vibrato := value
    else if (etype = ExtEventType.vibratoSpeed)
        UI.vibratoSpeed := value
    else if (etype = ExtEventType.vibratoDepth)
        UI.vibratoDepth := value
    end if
end function

function uiFeedbackPlayMode
    uiFeedback(ExtEventType.letRing, playModeGlobal.letRing)
    uiFeedback(ExtEventType.rh_Muted, playModeGlobal.rh_Muted)
    uiFeedback(ExtEventType.altStroke, playModeGlobal.altStroke)
    uiFeedback(ExtEventType.lh_Vibrato, playModeGlobal.lh_Vibrato)
end function

on controller
// debug("Before CC")
// debug_Playing()
    select ($CC_NUM)

        { Alt Stroke }
        case 20
            if (%CC[$CC_NUM] > 0)
                playModeGlobal.pickDown := 0 {every cc event should start pick down. As pickDown inverts on note start we should set it in 0}
                playModeGlobal.altStroke := 1
                UI.pickVelocityControl := 0
            else
                playModeGlobal.pickDown := 1
                playModeGlobal.altStroke := 0
                UI.pickVelocityControl := 1
            end if

        { Vibrato Trigger }
        case 21
            if (in_range(%CC[$CC_NUM], 1, 127))
                Playing_vibratoTrigger(1)
            else 
                Playing_vibratoTrigger(0)
            end if

        { Stop Strings }
        case 22
            if (in_range(%CC[$CC_NUM], 1, 6))
                Playing_stopNotesOnString(%CC[$CC_NUM])
            else
                Playing_stopAllNotes()
            end if

        { Let Ring }
        case 23
            if (%CC[$CC_NUM] = 0)
                PlayMode_change(playModeGlobal, ExtEventType.letRing, 0)
            else
                PlayMode_change(playModeGlobal, ExtEventType.letRing, 1)
            end if

        { Vibrato Speed }
        case 24
            extControl(ExtEventType.vibratoSpeed, %CC[$CC_NUM])

        { Vibrato Depth }
        case 25
            extControl(ExtEventType.vibratoDepth, %CC[$CC_NUM])

        { Slide In }
        case 26
            if (%CC[$CC_NUM] = 0)
                PlayMode_change(playModeGlobal, ExtEventType.slideIn, 0)
            else
                PlayMode_change(playModeGlobal, ExtEventType.slideIn, 1)
            end if

        { Slide Out }
        case 26
            if (%CC[$CC_NUM] = 0)
                PlayMode_change(playModeGlobal, ExtEventType.slideOut, 0)
            else
                PlayMode_change(playModeGlobal, ExtEventType.slideOut, 1)
            end if

    end select

    {debug_clear()
    debug_Strings()}
    // debug("After CC")
    // debug_Playing()
    uiFeedbackPlayMode()
end on

on listener
    select $NI_SIGNAL_TYPE
        case $NI_SIGNAL_TRANSP_STOP
            Playing_stopAllNotes()

        case $NI_SIGNAL_TIMER_MS
            // happens every 1ms
            
            timeOperations()

    end select
end on

function Control_getAttackSoftness() -> result
    result := UI.attackSoftness * 300
end function

function timeOperations()
    declare i
    declare now := Funcs.Time_getCurrentTime() // ms

    for i := 0 to Globals.maxVoices-1

        if (Playing[i].sys # -1 and Playing[i].ghost = 0)
{
            if (Playing[i].timeOp.fadeIn # -1 and Playing[i].active = 1)

                if (now - (Playing[i].startTime + Playing[i].timeOp.fadeIn) >= timeOperationPeriods.fadeIn)

                    declare offset
                    offset := Funcs.Time_linear(timeOperationValues.fadeIn_start, timeOperationValues.fadeIn_end, timeOperationTotals.fadeIn) * timeOperationPeriods.fadeIn
                    if (Playing[i].timeOp.fadeIn >= timeOperationTotals.fadeIn)
                        change_vol(Playing[i].sys, timeOperationValues.fadeIn_end, 0) // increasing by 1db
                        Playing[i].timeOp.fadeIn := -1
                    else 
                        change_vol(Playing[i].sys, offset, 1) // increasing by 1db
                        Playing[i].timeOp.fadeIn := Playing[i].timeOp.fadeIn + timeOperationPeriods.fadeIn
                    end if

                end if

            end if
}

            if (Playing[i].timeOp.vibrato # -1)
                if (now - (Playing[i].startTime + Playing[i].timeOp.vibrato) >= timeOperationPeriods.vibrato)

                    declare val
                    declare speed := playModeGlobal.vibratoSpeed

                    if (speed = 0)
                        val := 0
                    else 
                        declare alpha := Playing[i].timeOp.vibrato * (9000 / (101 - speed)) // 9000 because of degree type; 500
                        declare a := (timeOperationValues.vibratoDepthMax / 100) * playModeGlobal.vibratoDepth
                        // val := Sin(alpha)
                        val := val * (a / 10000) // sin returns from -10^4 to 10^4
                    end if                    

                    change_tune(Playing[i].sys, val, 0)
                    Playing[i].timeOp.vibrato := Playing[i].timeOp.vibrato + timeOperationPeriods.vibrato

                end if
            end if

            if (Playing[i].timeOp.pitchTable # -1)

                declare timer := now - Playing[i].timeOp.pitchTable
                declare totalStepCount
                totalStepCount := pitchTables[Globals.pitchTableIndex].timeLength * 100 / timeOperationPeriods.pitchTable // time length is in range 1-100, which is 0.1-10 seconds. In order to get milliseconds - multiply by 100

                if (timer >= totalStepCount)
                    Playing[i].timeOp.pitchTable := -1 // time operation finished
                else if (timer mod timeOperationPeriods.pitchTable = 0)

                    declare columnStepCount := totalStepCount / Const.pitchTableColumnCount

                    declare val
                    declare val_next := 0
                    declare column_index := timer / (columnStepCount * timeOperationPeriods.pitchTable)
                    declare cur_step := (timer / timeOperationPeriods.pitchTable) mod columnStepCount

                    val := pitchTables[Globals.pitchTableIndex].columns[column_index]

                    if (column_index < Const.pitchTableColumnCount-1)
                        val_next := pitchTables[Globals.pitchTableIndex].columns[column_index+1]
                    end if

                    val := pitchTables[Globals.pitchTableIndex].pitchRange * val + (pitchTables[Globals.pitchTableIndex].pitchRange * (val_next - val) / columnStepCount) * cur_step

                    change_tune(Playing[i].sys, val, 0)
{ if (cur_step = 0)
 debug2(val & " - ci" & column_index & " csc" & columnStepCount & " TSC" & totalStepCount)
end if
 }
                end if

            end if

        end if

    end for    
end function
